/*
 * Copyright Â© 2025 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

ext {
    goFiles = fileTree('.') {
        include 'internal/**/*.go'
        include 'basicauth.go'
    }
}

configurations {
    toolkitGo {
        canBeConsumed = false
        canBeResolved = true
    }
    
    goSource {
        canBeConsumed = true
        canBeResolved = false
    }
    
    basicauth {
        canBeConsumed = true
        canBeResolved = false
    }
}

dependencies {
    toolkitGo project(path: ':toolkit:go', configuration: 'goSource')
}

task test(type: Exec, dependsOn: [':toolkit:go:protoc']) {
    inputs.files(configurations.toolkitGo)
    inputs.files(goFiles)
    
    workingDir '.'
    executable 'go'
    args 'test'
    args './internal/...'
    args '-timeout=30s'
    if (project.findProperty('verboseTests') == 'true') {
        args '-v'
    }
}

task buildGo(type: GoLib, dependsOn: [':toolkit:go:protoc']) {
    inputs.files(configurations.toolkitGo)
    baseName 'basicauth'
    sources goFiles
    mainFile 'basicauth.go'
}

task build {
    dependsOn test
}

task clean(type: Delete) {
    delete "${buildDir}"
}

task assemble {
    dependsOn buildGo
}

dependencies {
    basicauth files(buildGo)
    goSource files(goFiles)
}

